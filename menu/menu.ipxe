#!ipxe


###################### MAIN MENU ######################

:main_menu
menu Welcome to iPXE Deployment Service support for platform ${platform}
item

item --gap --                           --- OS installation ---

item --key w windows			Install WINDOWS
item --key l linux                      Install LINUX (RHEL, SUSE ...)
item

item --gap --                           --- Advanced options ---
item --key c config                     show system information
item --key s shell                      drop to iPXE shell
item --key r reboot                     reboot system

item
item --key 0x08 exit        		Exit Service and continue boot

choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
goto ${selected}

###################### Defined Command ######################

:config
config
goto main_menu

:back
goto main_menu

:cancel
echo You cancelled the menu, dropping to shell

:shell
echo Type 'exit', back to the main menu
shell
goto main_menu

:failed
echo Booting failed, drop to iPXE shell
goto shell

:reboot
reboot

:exit
exit

############ MAIN MENU ITEMS ############

:windows
iseq ${platform} pcbios && goto windows-pcbios ||
iseq ${platform} efi && goto windows-efi ||
goto failed

:windows-pcbios
set net0/next-server 192.168.87.10
set net0/filename boot\x64\wdsnbp.com
chain ${tftp-boot-url}\x64\wdsnbp.com || goto failed

:windows-efi
set net0/next-server 192.168.87.10
set net0/filename boot\x64\wdsmgfw.efi
chain ${tftp-boot-url}\x64\wdsmgfw.efi || goto failed

:linux
chain ${http-menu-url}/linux/menu.ipxe || goto failed

