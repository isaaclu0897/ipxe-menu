#!ipxe


###################### MAIN MENU ######################

:main_menu
menu Welcome to iPXE Deployment Service support for platform ${platform}
item

item --gap --                           --- OS installation ---
item --key w windows			Install WINDOWS
item --key l linux                      Install LINUX (RHEL, SUSE ...)
item

item --gap --				--- Tools ---
item gparted				Gparted
item

item --gap --                           --- Advanced options ---
item --key c config                     show system information
item --key s shell                      drop to iPXE shell
item --key r reboot                     reboot system
item reload				reload iPXE

item
item --key 0x08 exit        		Exit Service and continue boot

choose --timeout ${menu-timeout} --default ${menu-default} selected || goto main_menu # if you cancel it, you will back to main menu
goto ${selected}

###################### Defined Command ######################

:config
config
goto back

:failed
echo Booting failed, drop to iPXE shell
# then keep going to shell
:shell
echo Type 'exit', back to the main menu
shell
# then keep going to back
:back
goto main_menu

:reboot
reboot

:reload
chain ${http-menu-url}/menu.ipxe

:exit
exit

############ MAIN MENU ITEMS ############

:windows
iseq ${platform} pcbios && goto windows-pcbios ||
iseq ${platform} efi && goto windows-efi ||
goto failed

:windows-pcbios
set net0/next-server 192.168.87.10
set net0/filename boot\x64\wdsnbp.com
chain ${tftp-boot-url}\x64\wdsnbp.com || goto failed

:windows-efi
set net0/next-server 192.168.87.10
set net0/filename boot\x64\wdsmgfw.efi
chain ${tftp-boot-url}\x64\wdsmgfw.efi || goto failed

:linux
chain ${http-menu-url}/linux/menu.ipxe || goto failed

# gparted -> uefi pass, legacy failed
:gparted
echo Booting Gparted Live
kernel ${http-images-file-url}/gparted/1_4_0_5/live/vmlinuz initrd=initrd.img boot=live config components union=overlay username=user noswap noeject vga=788 fetch=${http-images-file-url}/gparted/1_4_0_5/live/filesystem.squashfs
initrd ${http-images-file-url}/gparted/1_4_0_5/live/initrd.img
shell || goto failed
boot || goto failed

# Note (important)
# In the HyperV, I used legacy network adapter as PXE client boot network,
# but ... it is always failed at the stage of linux kernel boot, no matter is ubuntu redhat gparted
# -> you need to know it will successed on the real machine
# 
# Finally, I try to add network adapter on the hyperV, so the VM has two network interface,
# actually I don't know why! But ... it works haha...,
# if you have the same situation as me, maybe you can try to add a extra network

# root@ubuntu-Virtual-Machine:/var/www/ds# ll images_file/gparted/1_4_0_5/live/
# total 450031
# dr-xr-xr-x 1 root root      2048  七  15 07:48 ./
# dr-xr-xr-x 1 root root      2048  七  15 07:48 ../
# -r--r--r-- 1 root root     17270  七  15 07:39 filesystem.packages
# -r--r--r-- 1 root root        93  七  15 07:39 filesystem.packages-remove
# -r--r--r-- 1 root root 413302784  七  15 07:39 filesystem.squashfs
# -r--r--r-- 1 root root       872  七  15 07:48 GParted-Live-Version
# -r--r--r-- 1 root root  39813692  七  15 07:39 initrd.img
# -r--r--r-- 1 root root    182704  六  26  2015 memtest
# -r--r--r-- 1 root root   7508256  六  17 05:40 vmlinuz
# 
