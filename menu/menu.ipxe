#!ipxe

# #############################################
# Author: 		ISAAC
# Project:		QADS
# Create Date: 	2024.03.27
# #############################################

# ---

set menu-branch master
set menu-version 0.0.1

iseq ${platform} pcbios && colour --basic 0 4 ||

###################### MAIN MENU ######################

:mainMenu
menu Welcome to iPXE Deployment Service support for platform ${platform}
item --gap --							${spx8}${spx8}${spx8}Menu Version: ${menu-branch}: ${menu-version}
item

item --gap --                           --- OS installation ---
item --key w windows                    (w) WINDOWS
item --key l linux                      (l) LINUX (RHEL, OL, SUSE ...)
item --key v vmware                     (v) VMWARE

item --gap --							--- Tools ---
item gparted							Gparted
item
item --key 0x08 exit                    Exit Service and continue boot
item

item --gap --                           --- Advanced options ---
item --key c config						(c) config
item --key s shell						(s) shell
item --key r reload						(r) reload
item ipxeEfi							ipxeEfi (default for efi)
item snponlyEfi							snponlyEfi
item ipxePxe							ipxePxe (default for pcbios)
item undionlyKpxe						undionlyKpxe

item
item --key 0x08 exit        		Exit Service and continue boot

choose --timeout ${menu-timeout} --default ${menu-default} selected || goto failed
goto ${selected}

###################### System Information ######################

:systemInfo
menu System Information
item

item --gap -- 							--- iPXE ---
item -- --								Platform:	${platform}
item -- --								ARCH:		${buildarch}
item -- --								Version:	${version}
item

item --gap -- 							--- SUT ---
item -- --								MAC: 		${netX/mac:hex}
item -- --								IP:			${netX/ip}
item

item back								Back to previous page
choose --timeout ${submenu-timeout} --default ${submenu-default} selected || goto back
goto mainMenu 

###################### Defined Command ######################

:info
goto systemInfo

:config
config
goto mainMenu

:cancel
echo You cancelled the menu, dropping to shell

:shell
echo Type 'exit', back to the main menu
shell
goto reload

:failed
echo Booting failed, drop to iPXE shell
goto shell

:reboot
reboot

:exit
exit

############ MAIN MENU ITEMS ############

:reload
chain -r ${tftp-ipxe-url}/menu.ipxe || goto failed

:windows
iseq ${platform} pcbios && goto windows-pcbios ||
iseq ${platform} efi && goto windows-efi ||
goto failed

:windows-pcbios
chain ${tftp-boot-url}/x64/wdsnbp.com || goto failed

:windows-efi
chain ${tftp-boot-url}/x64/wdsmgfw.efi || goto failed

:linux
chain ${tftp-ipxe-url}/Linux/menu.ipxe || goto failed

:vmware
chain ${tftp-ipxe-url}/VMware/menu.ipxe || goto failed

:back
chain ${tftp-ipxe-url}/menu.ipxe || goto failed

:ipxeEfi
chain -r ${tftp-ipxe-url}/bin/ipxe.efi

:snponlyEfi
chain -r ${tftp-ipxe-url}/bin/snponly.efi

:ipxePxe
chain -r ${tftp-ipxe-url}/bin/ipxe.pxe

:undionlyKpxe
chain -r ${tftp-ipxe-url}/bin/undionly.kpxe

# gparted -> uefi pass, legacy failed
:gparted
echo Booting Gparted Live
kernel ${http-images-file-url}/gparted/1_4_0_5/live/vmlinuz initrd=initrd.img boot=live config components union=overlay username=user noswap noeject vga=788 fetch=${http-images-file-url}/gparted/1_4_0_5/live/filesystem.squashfs
initrd ${http-images-file-url}/gparted/1_4_0_5/live/initrd.img
boot || goto failed

# Note (important)
# In the HyperV, I used legacy network adapter as PXE client boot network,
# but ... it is always failed at the stage of linux kernel boot, no matter is ubuntu redhat gparted
# -> you need to know it will successed on the real machine
# 
# Finally, I try to add network adapter on the hyperV, so the VM has two network interface,
# actually I don't know why! But ... it works haha...,
# if you have the same situation as me, maybe you can try to add a extra network

# root@ubuntu-Virtual-Machine:/var/www/ds# ll images_file/gparted/1_4_0_5/live/
# total 450031
# dr-xr-xr-x 1 root root      2048  七  15 07:48 ./
# dr-xr-xr-x 1 root root      2048  七  15 07:48 ../
# -r--r--r-- 1 root root     17270  七  15 07:39 filesystem.packages
# -r--r--r-- 1 root root        93  七  15 07:39 filesystem.packages-remove
# -r--r--r-- 1 root root 413302784  七  15 07:39 filesystem.squashfs
# -r--r--r-- 1 root root       872  七  15 07:48 GParted-Live-Version
# -r--r--r-- 1 root root  39813692  七  15 07:39 initrd.img
# -r--r--r-- 1 root root    182704  六  26  2015 memtest
# -r--r--r-- 1 root root   7508256  六  17 05:40 vmlinuz
# 
